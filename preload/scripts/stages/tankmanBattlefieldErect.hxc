import flixel.FlxG;
import flixel.math.FlxAngle;
import funkin.play.PlayState;
import funkin.play.stage.Stage;
import funkin.graphics.shaders.DropShadowShader;
import funkin.play.character.CharacterType;
import openfl.display.BitmapData;
import flixel.graphics.FlxGraphic;

class TankmanBattlefieldErectStage extends Stage
{
  function new()
  {
    super('tankmanBattlefieldErect');
  }

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);
  }

  var tankmanRim:DropShadowShader;

  public function onGameOver(event:ScriptEvent)
  {
    super.onGameOver(event);
    getBoyfriend().shader = null;
  }

  override function addCharacter(character:BaseCharacter, charType:CharacterType):Void
  {
    // Apply the shader automatically to each character as it gets added.
    super.addCharacter(character, charType);
    trace('Applied stage shader to ' + character.characterName);

    var rim = new DropShadowShader();
    rim.setAdjustColor(-46, -38, -25, -20);
    rim.color = 0xFFDFEF3C;
    character.shader = rim;
    rim.attachedSprite = character;

    if (character.isAnimate)
    {
      character.useRenderTexture = true;

      // This is to properly intitialize the shader itself
      // If this isn't here, the character will flash piss yellow briefly before the next animation frame!!
      rim.updateFrameInfoSimple(character.frameWidth, character.frameHeight, 0);
    }

    switch (charType)
    {
      case CharacterType.BF:
        rim.angle = 90;

      case CharacterType.GF:
        rim.angle = 90;

        if (getGirlfriend().characterId == 'gf-tankmen' || getGirlfriend().characterId == 'nene-tankmen')
        {
          if (getGirlfriend().characterId == 'gf-tankmen') rim.loadAltMask(Paths.image('erect/masks/gfTankmen_mask'));
          else
            rim.loadAltMask(Paths.image('erect/masks/neneTankmen_mask'));
          rim.maskThreshold = 0.4;
          rim.useAltMask = true;
        }

        if (getGirlfriend().characterId == 'otis-speaker')
        {
          getNamedProp('tankBricks').setPosition(445, 774);
        }

      case CharacterType.DAD:
        rim.angle = 135;
        rim.threshold = 0.3;

        if (getDad().characterId == 'tankman-bloody')
        {
          rim.loadAltMask(Paths.image('erect/masks/tankmanCaptainBloody_mask'));
          rim.maskThreshold = 1;
          rim.useAltMask = false;
        }

        // need to save this for later for when the mask is needed
        tankmanRim = rim;

      default:
        // Do nothing.
    }

    // TODO: This is here because of a Polymod bug!!
    var character_persist:BaseCharacter = character;

    character.animation.onFrameChange.add(function() {
      if (character_persist != null)
      {
        var frameAngle:Float = !character_persist.isAnimate ? character_persist.frame.angle : 0;
        rim.updateFrameInfoSimple(character_persist.frameWidth, character_persist.frameHeight, frameAngle);
      }
    });
  }

  /**
   * Called when the chart hits a song event.
   */
  public override function onSongEvent(scriptEvent:SongEventScriptEvent)
  {
    super.onSongEvent(scriptEvent);

    if (scriptEvent.eventData.eventKind == "EnableMask" && tankmanRim != null)
    {
      tankmanRim.useAltMask = true;
    }
  }

  function onUpdate(event:UpdateScriptEvent):Void
  {
    super.onUpdate(event);
  }

  function onBeatHit(event:SongTimeScriptEvent):Void
  {
    super.onBeatHit(event);

    if (FlxG.random.bool(2))
    {
      getNamedProp('sniper').playAnimation('sip', false, true);
    }
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);

    if (tankmanRim != null)
    {
      tankmanRim.useAltMask = false;
    }

    if (getGirlfriend().characterId == 'otis-speaker')
    {
      getNamedProp('tankBricks').setPosition(445, 774);
    }
  }
}
